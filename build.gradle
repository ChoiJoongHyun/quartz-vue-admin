plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE'
    id 'java'
    id "com.moowork.node" version "1.3.1"
}

apply plugin: 'io.spring.dependency-management'

group = 'com.quartz'
version = '1.0.0'
sourceCompatibility = '1.8'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

node {
    // Version of node to use.
    version = '8.11.3'

    // Base URL for fetching node distributions (change if you have a mirror).
    distBaseUrl = 'https://nodejs.org/dist'

    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true

    // Set the work directory where node_modules should be located
    nodeModulesDir = file('src/main/frontend')
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

//when bootJar task execute, jar file is renamed. ex) quartz-vue-admin-1.0.0-20190830-193819.jar
bootJar {
    archiveBaseName = project.name
    archiveVersion = project.version + '-' + new Date().format('yyyyMMdd-HHmmss')
}

//npmInstallThenBuild task. npm install and npm run build execute.
task npmInstallThenBuild(dependsOn: [npmInstall, npm_run_build])

//execute npmInstallThenBuild before processResources
processResources.dependsOn npmInstallThenBuild